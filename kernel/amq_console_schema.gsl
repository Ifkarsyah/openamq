#   This script extracts the console schema by walking
#   the amq class definitions, starting with amq_broker.

schema_name    = "openamq.org/kernel"
schema_version = "0.2"

#   Formatting
shuffle = 0
indent  = "   "
ignorecase = 0

function process_field (field, margin)
    >$(my.margin)<field name = "$(name)"\
    if defined (field.label)
        > label = "$(label)"\
    endif
    if defined (field.type)
        > type = "$(type)"\
    endif
    if defined (field.repeat)
        > repeat = "$(repeat)"\
    endif
    if count (field.get) = 0
        > inspect = "0"\
    endif
    if count (field.put)
        > modify = "1"\
    endif
    > />
    for values
        echo "Field values are not supported yet"
    endfor
endfunction

function process_method (method, margin)
    >$(my.margin)<method name = "$(name)" label = "$(label?name)">
    for field
        process_field (field, my.margin + indent)
    endfor
    >$(my.margin)</method>
endfunction

function process_class (name, margin)
    xml from "amq_$(my.name).icl"
    for ->class.data where name = "cml"
        for class
            >$(my.margin)<class name = "$(name)" label = "$(label?name)">
            if count (field, name = "name") = 0
                echo "E: no 'name' defined for $(class.name)"
            endif
            for field
                process_field (field, my.margin + indent)
            endfor
            for method
                process_method (method, my.margin + indent)
            endfor
            >$(my.margin)</class>
            for field where field.type ?= "objref"
                if count (root.class, name = field.name) = 0
                    copy field to root as class
                    process_class (name, my.margin)
                endif
            endfor
        endfor
    endfor
endfunction

output "amq_console_schema.cml"
><?xml version="1.0"?>
><cml xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
> xsi:schemaLocation="http://www.openamq.org/schema/cml cml.xsd"
> xmlns="http://www.openamq.org/schema/cml"
> version="1.0">
><schema-reply name = "$(schema_name)" version = "$(schema_version)" status = "ok">
process_class ("broker", indent)
></schema-reply>
></cml>
