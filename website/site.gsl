.template 0
#
#   OpenAMQ website generator
#
#   Loads all pages, then runs template.gsl on each one.
#   Page content can be XHTML or Gurudoc or a combination of the two.
#

function report (message)
    if !defined (switches.quiet)
        echo my.message
    endif
endfunction

ignorecase = 0

#   Load page contents from external files
report ("Loading XML page data...")
for section
    for page
        xml to section from page.name
        delete page
    endfor
endfor

#   Convert page contents
report ("Converting page content...")
for section
    for page
        for content where type = "gurudoc"
            if !defined (content.filename)
                content.filename = "temp.txt"
                output content.filename
                >$(gsl.exec (content.?'', 1))
                close
            endif
            #   Convert gurudoc content to XHTML
            process = proc.new ("./gd2xhtml", ".", content.filename, "temp.xml")
            process.run ()
            new content to page
                content.load_file ("temp.xml")
            endnew
            delete content
        endfor
    endfor
endfor

#   Generate output
report ("Generating output HTML...")
for section
    for page
        output "$(page.name).html"
        include "template.gsl"
    endfor
endfor

.endtemplate
