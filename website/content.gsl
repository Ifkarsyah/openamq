#   Web site generation script
#   excuse humble code
#   it will be industrialised
#   at a later date

#   - last space in text is replaced by non-breaking spaces
#   - symbols are replaced
function format_block (args)
    if defined (my.args)
        my.count = 1
        my.index = string.length (my.args) - 1
        while my.index > 1 & my.count > 0
            if string.substr (my.args, my.index,, 1) = ' '
                my.args = string.substr (my.args, 0, my.index - 1) + "&nbsp;" + string.substr (my.args, my.index + 1, )
                my.count -= 1
            endif
            my.index -= 1
        endwhile
    elsif defined (content)
        for root->content.block where block.name = content & block.language = language.name
            my.args = block.
        endfor
    else
        my.args = "[Empty]"
    endif
    return (gsl.exec (my.args, 1))
endfunction

function set_page_next (name, next)
    if defined (my.name)
        for root-> content.page where name = my.name
            page.next = my.next
        endfor
    endif
endfunction

global.ignorecase = 0
slideshow = 0
xml from "content.xml"
for root->content.include
    xml to root->content from include.file
endfor

for root->content.page
    set_page_next (lastpage, page.name)
    for page.set
        page.$(name) = value
    endfor
    lastpage = page.name
    for language
        echo "Generating $(page.name)_$(language.name).html..."
        output "$(page.name)_$(language.name).html"
        include "template.gsl"
    endfor
endfor
#   Final page returns to index again
set_page_next (lastpage, root-> content-> page.name)

#   Generate slideshow pages
slideshow = 1
for root->content.page where slide ?= 1
    for language
        echo "Generating slide_$(page.name)_$(language.name).html..."
        output "slide_$(page.name)_$(language.name).html"
        include "template.gsl"
    endfor
endfor
